#!/usr/bin/env perl

use v5.38;
use lib 'lib';
use lib 'local/lib/perl5';

use Solution::External;
use Util;

binmode STDOUT, ':encoding(UTF-8)';

my $day = shift;
my @days;
if ($day && $day =~ m{ \A (\d+) (?: \.\. (\d+))? \z }x) {
	my $day_from = $1;
	my $day_to = $2 || $day_from;
	@days = $day_from .. $day_to;
}

die 'requires day number(s)'
	if !@days;

my $language = shift;
my $total_time = 0;

foreach my $day (@days) {
	my $obj;

	if ($language) {
		$obj = Solution::External->new(
			day => $day,
			language => $language,
		);
	}
	else {
		my $pkg = "Day${day}::Solution";
		eval "use $pkg; 1" or die "error loading solution for day $day:\n$@";

		$obj = $pkg->new;
	}

	$obj->part_1;
	$total_time += $obj->timer;

	$obj->part_2;
	$total_time += $obj->timer;
}

if (@days > 1) {
	Util->print_time($total_time, header => 'Total', time_multiplier => @days * 2);
}

